#!raku

use Sparrow6;
use Sparrow6::Task::Repository;
use Sparrow6::Task;
use Sparrow6::RakuTask;
use Sparrow6::SparrowTask;

sub MAIN (
  $thing?,
  Bool :$debug            = %*ENV<SP6_DEBUG> ?? True !! False,
  Bool :$verbose          = False,
  Bool :$version          = False,
  Bool :$clean            = False,
  Bool :$help             = False,
  Bool :$index-update     = False,
  Bool :$list             = False,
  Bool :$search           = False,
  Bool :$upload           = False,
  Bool :$repo-init        = False,
  Bool :$repo-info        = False,
  Bool :$install          = False,
  Bool :$uninstall        = False,
  Bool :$plg-run          = False,
  Bool :$plg-info         = False,
  Bool :$plg-man          = False,
  Bool :$task-run         = False,
  Bool :$task-cat         = False,
  Bool :$task-list        = False,
  Bool :$task-del         = False,
  Bool :$rt-run         = False,
  Bool :$rt-set         = False,
  Bool :$rt-cat         = False,
  Bool :$rt-list        = False,
  Bool :$rt-del         = False,
  Bool :$module-run       = False,
  Bool :$force            = False,
  Str  :$prefix,
)

{

  if $version {
    say $Sparrow6::VERSION;
    return;
  }

  my $repo-api = Sparrow6::Task::Repository::Api.new(
    debug   => $debug,
    prefix  => $prefix
  );

  my $task-cli = Sparrow6::Task::Cli.new(
    debug   => $debug,
    prefix  => $prefix
  );

  my $raku-task-cli = Sparrow6::RakuTask::Cli.new(
    debug   => $debug,
    prefix  => $prefix
  );

  my $sparrow-task-cli = Sparrow6::SparrowTask::Cli.new(
    debug   => $debug,
    prefix  => $prefix
  );

  if ($help) {
    $task-cli.help;
    exit;
  }

  if $repo-init {

    $repo-api.console-with-prefix("repo initialization");
    $repo-api.repo-init($thing);

  } elsif $repo-info {

    $repo-api.console-with-prefix("repo information");
    $repo-api.repo-info();

  } elsif $upload {

    $repo-api.console-with-prefix("upload plugin");

    $repo-api.plugin-upload( %( force => $force ) );

  } elsif $install && $thing {

    $repo-api.console-with-prefix("install plugin $thing");

    $repo-api.plugin-install($thing, %( force => $force, verbose => $verbose ));

  } elsif $uninstall && $thing {

    $repo-api.console-with-prefix("uninstall plugin $thing");

    $repo-api.plugin-uninstall($thing, %( verbose => $verbose ));

  } elsif $plg-info && $thing {

    $repo-api.console-with-prefix("plugin $thing info");

    $repo-api.plugin-info($thing, %( verbose => $verbose ));

  } elsif $task-run && $thing {

    $sparrow-task-cli.console-with-prefix("run sparrow task $thing");

    for $thing.split(/ "+" /) -> $i {
      $sparrow-task-cli.task-run($i);
    }

  } elsif $rt-run && $thing {

    $raku-task-cli.console-with-prefix("run raku task $thing");

    $raku-task-cli.task-run($thing);

  } elsif $module-run && $thing {

    $task-cli.console-with-prefix("run module $thing");

    for $thing.split(/ "+" /) -> $i {
      $task-cli.module-run($i);
    }

  } elsif $plg-run && $thing {

    $task-cli.console-with-prefix("run plg $thing");

    for $thing.split(/ "+" /) -> $i {
      $task-cli.plg-run($i);
    }

  } elsif $plg-man && $thing {

    $repo-api.console-with-prefix("man plg $thing");

    $repo-api.plugin-man($thing);

  } elsif $module-run && $thing {

    $task-cli.console-with-prefix("run module $thing");

  } elsif $rt-set && $thing {

    $raku-task-cli.console-with-prefix("set raku task $thing");

    $raku-task-cli.task-set($thing);

  } elsif $task-cat && $thing {

    $sparrow-task-cli.console-with-prefix("show sparrow task $thing");

    $sparrow-task-cli.task-cat($thing);

  } elsif $rt-cat && $thing {

    $raku-task-cli.console-with-prefix("show raku task $thing");

    $raku-task-cli.task-cat($thing);

  } elsif $task-del && $thing {

    $sparrow-task-cli.console-with-prefix("delete sparrow task $thing");

    $sparrow-task-cli.task-del($thing);

  } elsif $rt-del && $thing {

    $raku-task-cli.console-with-prefix("delete raku task $thing");

    $raku-task-cli.task-del($thing);

  } elsif $task-list {

    $sparrow-task-cli.console-with-prefix("list sparrow tasks");

    $sparrow-task-cli.task-list($thing||'.');

  } elsif $rt-list {

    $raku-task-cli.console-with-prefix("list raku tasks");

    $raku-task-cli.task-list;

  } elsif $index-update {

    $repo-api.console-with-prefix("update local index");

    $repo-api.index-update();

  } elsif $list {

    $repo-api.console-with-prefix("installed plugins");

    for $repo-api.installed-plugins() -> $i {
      say "{$i[0]} ... $i[1] ... $i[2]"
    }

  } elsif $search {

    $repo-api.console-with-prefix("search plugins");
    for $repo-api.search-plugins($thing) -> $i {
      if $i[1].defined {
        say "{$i[0]} ... {$i[1]} ... {$i[2]}"
      } else {
        say "{$i[0]} ... Nil ... {$i[2]}"
      }
    }
  } else {

    $task-cli.help();

    exit(10);

  }

}
